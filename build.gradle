plugins {
    id 'application'
    id 'java'
    id 'distribution'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

ext {
   javaMainClass = 'mochadoom.Engine'
}

jar {
    archiveBaseName = 'mochadoom'
    manifest {
        attributes('Main-Class': javaMainClass)
    }
}

application {
    mainClass.set(javaMainClass)
}

distributions {
    main {
        distributionBaseName = 'mochadoom'
        contents {
            from jar
            from 'wads'
            from("$projectDir") {
                include 'LICENSE.txt'
                include 'README.md'
                include 'CONFIG.md'
                include 'default.cfg'
                include 'classic.cfg'
                include 'mochadoom*.sh'
                include 'mochadoom*.bat'
            }
            into '/'
        }
    }
}

tasks.withType(JavaCompile).all {
    options.compilerArgs.add('-Xlint:unchecked')
    options.compilerArgs.add('-Xlint:deprecation')
}

distTar.enabled = false
distZip {
    eachFile { file ->
        if (file.path.matches('^(bin|lib)/.*$')) {
            file.exclude()
        }
    }
}

